plugins {
    // Spring boot & dependency management:
    // https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id "io.spring.dependency-management" version "1.0.11.RELEASE" 
    // Lombok generates getters and setters and more. https://projectlombok.org/setup/gradle
    // 5.3.3.3 is newest version compatible with Gradle 6 it seems.
    id "io.freefair.lombok" version "5.3.3.3"
    id 'java'
    id 'java-library'
    // testing, documentation, code coverage, ...
    id 'jacoco'
    id "org.owasp.dependencycheck" version "6.5.0.1"
    id "com.github.kt3k.coveralls" version "2.12.0" 
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

jar {
    archiveBaseName = 'template'
    archiveVersion =  System.getenv('version')
}

repositories {
    mavenLocal() 
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
 
configurations {
    all*.exclude module : 'spring-boot-starter-logging'
    // See testing & documentation at the end of this file
    asciidoctorExt
}

lombok {
    // check here for new versions: https://projectlombok.org/download
    version = '1.18.22'
}

if (System.getProperty("profile") == "minimal") {
    // Use "./gradlew -Dprofile=minimal build" if you want to finish the build task faster.
    // Might skip extensive tests or documentation creation.
    println "Using minimal profile for building " + project.getName()
    apply from: 'gradle/profile-minimal.gradle'
} else {
    println "Using default profile (full/release build) for " + project.getName()
    apply from: 'gradle/profile-default.gradle'
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-data-rest")
 
    implementation("edu.kit.datamanager:service-base:0.1.2")
    //Enable dependency if messaging is needed, otherwise reduce size by skipping it
    //implementation("edu.kit.datamanager:generic-message-consumer:0.1")

    //Java 11 Support 
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.0.1'
    implementation('javax.activation:activation:1.1.1')
    implementation('org.javassist:javassist:3.23.1-GA')
    
    //Java 11 Support 
    testImplementation("org.mockito:mockito-core:2.18.0")
    testImplementation("org.powermock:powermock-module-junit4:2.0.0-beta.5")
    testImplementation("org.powermock:powermock-api-mockito2:2.0.0-beta.5")

    // Asciidoctor + spring-restdocs, see at the end of this file.
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
    asciidoctorExt('org.springframework.restdocs:spring-restdocs-asciidoctor')
    asciidoctorExt('com.github.allati.asciidoctor.monotree:asciidoctor-extension-monotree:0.0.1')
}

bootJar {
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    launchScript()
}
